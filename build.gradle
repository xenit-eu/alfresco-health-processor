plugins {
    id 'eu.xenit.amp' version '1.0.1' apply(false)
    id 'eu.xenit.alfresco' version '1.0.1' apply(false)
    id 'eu.xenit.docker-alfresco' version '5.0.6' apply(false)
    id 'eu.xenit.docker-compose.auto' version '5.0.6' apply(false)
    id "be.vbgn.ci-detect" version "0.5.0"
    id "org.sonarqube" version "3.1.1"
    id "org.kordamp.gradle.jacoco" version "0.43.0"
}

sonarqube {
    properties {
        property "sonar.projectKey", "xenit-eu_alfresco-health-processor"
        property "sonar.organization", "xenit-eu"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.exclusions", "**/eu/xenit/alfresco/healthprocessor/example/*"
    }
}

ext {
    alfrescoVersion = '5.2.f'
    alfredTelemetryVersion = '0.4.0'
    micrometerVersion = '1.0.6'
    lombokVersion = '1.18.10'
    enterpriseEnabled = rootProject.hasProperty("enterprise")
}

subprojects {
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    ext {
        junitJupiterVersion = '5.6.2'
        mockitoVersion = '3.3.3'
        hamcrestVersion = '1.3'
        restAssuredVersion = '4.0.0'
        awaitilityVersion = '4.0.3'
    }
}

allprojects { Project p ->
    group = 'eu.xenit.alfresco'
    version = '0.2.0'

    boolean isRelease = ci.branch?.startsWith("release")
    if (!isRelease) {
        version += "-SNAPSHOT"
    }

    repositories {
        mavenCentral()
        maven {
            url "${rootProject.projectDir}/alfresco-bom-repo"
        }
        maven {
            url 'https://artifacts.alfresco.com/nexus/content/groups/public'
        }
        if(p.name.contains("enterprise") && enterpriseEnabled) {
            maven {
                url 'https://artifactory.xenit.eu/artifactory/alfresco-enterprise-cache'
                credentials {
                    username property("eu.xenit.artifactory.username")
                    password property("eu.xenit.artifactory.password")
                }
            }
        }
    }
}