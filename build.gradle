plugins {
    id "io.codearte.nexus-staging" version "0.21.2"
    id 'eu.xenit.amp' version '1.0.1' apply(false)
    id 'eu.xenit.alfresco' version '1.0.1' apply(false)
    id 'eu.xenit.docker-alfresco' version '5.0.6' apply(false)
    id 'eu.xenit.docker-compose.auto' version '5.0.6' apply(false)
    id "org.sonarqube" version "3.1.1"
    id "org.kordamp.gradle.jacoco" version "0.43.0"
}

nexusStaging {
    username = project.hasProperty('publish_username') ? project.publish_username : ''
    password = project.hasProperty('publish_password') ? project.publish_password : ''
}

sonarqube {
    properties {
        property "sonar.projectKey", "xenit-eu_alfresco-health-processor"
        property "sonar.organization", "xenit-eu"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

ext {
    shortDotLessAlfrescoVersion = '61'
    alfrescoVersion = '6.1.2-ga'
    care4alfModulesVersion = '2.1.0'
    lombokVersion = '1.18.10'
    enterpriseEnabled = rootProject.hasProperty("enterprise")
}

subprojects {
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    ext {
        junitJupiterVersion = '5.6.2'
        mockitoVersion = '3.3.3'
        hamcrestVersion = '1.3'
        restAssuredVersion = '4.3.0'
    }

    apply from: "${rootProject.projectDir}/publish.gradle"
}

allprojects { Project p ->
    group = 'eu.xenit.alfresco'
    version = '0.1.0'

    boolean isRelease = System.env.BRANCH_NAME?.startsWith("release")
    if (!isRelease) {
        version += "-SNAPSHOT"
    }

    repositories {
        mavenCentral()
        maven {
            url "${rootProject.projectDir}/alfresco-bom-repo"
        }
        maven {
            url 'https://artifacts.alfresco.com/nexus/content/groups/public'
        }
        if(p.name.contains("enterprise") && enterpriseEnabled) {
            maven {
                url 'https://artifactory.xenit.eu/artifactory/alfresco-enterprise-cache'
                credentials {
                    username property("eu.xenit.artifactory.username")
                    password property("eu.xenit.artifactory.password")
                }
            }
        }
    }
}