plugins {
    id 'eu.xenit.alfresco' version '1.1.0' apply(false)
    id 'eu.xenit.docker-alfresco' version '5.3.0' apply(false)
    id "be.vbgn.ci-detect" version "0.5.0"
    id "org.sonarqube" version "3.1.1"
    id "org.kordamp.gradle.jacoco" version "0.43.0"
}

sonarqube {
    properties {
        property "sonar.projectKey", "xenit-eu_alfresco-health-processor"
        property "sonar.organization", "xenit-eu"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.exclusions", "**/eu/xenit/alfresco/healthprocessor/example/*"
    }
}

ext {
    alfrescoVersion = '5.2.f'
    alfredTelemetryVersion = '0.6.0'
    micrometerVersion = '1.0.6'
    lombokVersion = '1.18.10'
}

subprojects {
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    ext {
        junitJupiterVersion = '5.6.2'
        mockitoVersion = '3.3.3'
        hamcrestVersion = '1.3'
        restAssuredVersion = '4.0.0'
        awaitilityVersion = '4.0.3'
    }
}

def copyPropertyValueIfExists(sourcePropertyName, targetPropertyName) {
    if (project.hasProperty(sourcePropertyName)) {
        project.ext[targetPropertyName] = project.property(sourcePropertyName)
    }
}

allprojects { Project p ->
    group = 'eu.xenit.alfresco'
    version = '0.4.1'

    boolean isRelease = ci.branch?.startsWith("release")
    if (!isRelease) {
        version += "-SNAPSHOT"
    }

    // It is not possible to set properties with a dot via GitHub Actions env variables, therefore we introduce support
    // for a non-dotted-equivalent
    copyPropertyValueIfExists('alfresco_nexus_username', 'org.alfresco.maven.nexus.username')
    copyPropertyValueIfExists('alfresco_nexus_password', 'org.alfresco.maven.nexus.password')
    copyPropertyValueIfExists('xenit_docker_registry_url', 'eu.xenit.docker.registry.url')
    copyPropertyValueIfExists('xenit_docker_registry_username', 'eu.xenit.docker.registry.username')
    copyPropertyValueIfExists('xenit_docker_registry_password', 'eu.xenit.docker.registry.password')

    repositories {
        mavenCentral()
        maven {
            url "${rootProject.projectDir}/alfresco-bom-repo"
        }
        maven {
            url 'https://artifacts.alfresco.com/nexus/content/groups/public'
        }
        if(p.name.contains("enterprise")) {
            maven {
                url 'https://artifacts.alfresco.com/nexus/content/groups/private'
                credentials {
                    username project.property('org.alfresco.maven.nexus.username')
                    password project.property('org.alfresco.maven.nexus.password')
                }
            }
        }
    }
}
