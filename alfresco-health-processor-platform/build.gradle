plugins {
    id 'idea'
    id 'java'
    id 'eu.xenit.alfresco' version "1.0.1"
    id 'eu.xenit.amp' version "1.0.1"
}

//description = "Extension based upon the Alfresco Solr Api endpoints, required to perform an Alfresco to Alfresco " +
//        "migration with Alfred Inflow."

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

ext {
    alfrescoVersion = '5.0.d'
}

dependencies {
    alfrescoProvided("org.alfresco:alfresco-core:${alfrescoVersion}") { transitive = false }
    alfrescoProvided("org.alfresco:alfresco-remote-api:${alfrescoVersion}") { transitive = false }
    alfrescoProvided("org.alfresco:alfresco-data-model:${alfrescoVersion}") { transitive = false }
    alfrescoProvided("org.alfresco:alfresco-repository:${alfrescoVersion}") { transitive = false }
    alfrescoProvided('org.springframework.extensions.surf:spring-webscripts:1.2.0-M35') { transitive = false }
    alfrescoProvided('org.springframework.extensions.surf:spring-surf-core:1.2.0-M35') { transitive = false }
    alfrescoProvided 'org.json:json:20090211'
    alfrescoProvided 'org.slf4j:slf4j-api:1.7.7'
}

processResources {
    filesMatching("**/module.properties") {
        expand project.properties
    }
}

sourceSets {
    main {
        amp {
            module("src/main/resources/alfresco/module/alfresco-solr-api-extended-platform/module.properties")
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact amp
        }
    }
}

