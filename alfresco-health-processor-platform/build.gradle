plugins {
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

description = "A background processor capable of performing various operations on the nodes in your Alfresco repository, in a batched and controlled manner"

apply plugin: "idea"
apply plugin: 'java'
apply plugin: "eu.xenit.alfresco"
apply plugin: "eu.xenit.amp"

configurations {
    ampLib
}

java {
    jar.archiveClassifier.set('unbundled')
    sourceCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    alfrescoProvided(platform("eu.xenit.alfresco:alfresco-community-bom:${alfrescoVersion}"))
    alfrescoProvided("org.alfresco:alfresco-repository") {
        exclude module: 'spring-social-facebook-web'
    }
    alfrescoProvided "io.micrometer:micrometer-core:${micrometerVersion}"
    implementation 'com.google.guava:guava:30.1-jre'

    ampLib project(":alfresco-health-processor-platform:alfresco-7-upwards")

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "org.hamcrest:hamcrest-all:${hamcrestVersion}"
    testImplementation 'org.slf4j:slf4j-simple:1.7.25'
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "shadow.eu.xenit.alfresco.healthprocessor"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar
tasks.assemble.dependsOn tasks.shadowJar

shadowJar {
    minimize()
    archiveClassifier.set('')
}

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
}

amp {
    libs = shadowJar.outputs.files + configurations.ampLib
}

apply from: "${rootDir}/gradle/publish.gradle"

publishing {
    publications {
        java(MavenPublication) {
            artifact tasks.shadowJar
            artifact tasks.javadocJar
            artifact tasks.sourcesJar
            artifact amp
        }
    }
}