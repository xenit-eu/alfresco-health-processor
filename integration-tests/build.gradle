plugins {
    id 'java'
    id 'eu.xenit.alfresco'
    id 'eu.xenit.amp'
}

ext {
    slf4jVersion = '1.7.36'
    //Default overloaded in subprojects.
    //docker-compose_solr_ssl.yml
    integrationTestComposeFile = 'docker-compose_solr_secret.yml'
}

dependencies {
    alfrescoProvided project(":alfresco-health-processor-platform")
    alfrescoProvided(enforcedPlatform("eu.xenit.alfresco:alfresco-community-bom:${alfrescoVersion}"))
    alfrescoProvided("org.alfresco:alfresco-repository") {
        exclude module: 'spring-social-facebook-web'
    }


    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "org.hamcrest:hamcrest-all:${hamcrestVersion}"
    testImplementation "io.rest-assured:rest-assured:${restAssuredVersion}"
    testImplementation "org.awaitility:awaitility:${awaitilityVersion}"

    testImplementation group: 'org.slf4j', name: 'slf4j-api', version: "${slf4jVersion}"
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: "${slf4jVersion}"

}

test {
    // By default tests should only run in the subprojects. To manually run tests in your IDEA, uncomment:
    enabled = false
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

subprojects { Project p ->
    apply plugin: 'java'

    def alfrescoVersion = project.name[-2..-1]
    apply from: "${project.projectDir}/overload.gradle"

    description = "Alfresco ${alfrescoVersion} with Health Processor"

    task integrationTest(type: Test) {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
        testClassesDirs = project.parent.sourceSets.test.output.classesDirs
        classpath = project.parent.sourceSets.test.runtimeClasspath
        outputs.upToDateWhen { false }
    }

    check.dependsOn integrationTest

    apply plugin: 'eu.xenit.docker-alfresco'
    apply plugin: 'eu.xenit.docker-compose.auto'

    dependencies {
        if (project.hasProperty("alfrescoBaseWarBom")) {
            baseAlfrescoWar platform("${alfrescoBaseWarBom}")
        }
        baseAlfrescoWar "${alfrescoBaseWar}"
        alfrescoSM group: 'org.postgresql', name: 'postgresql', version: '42.7.4'
        alfrescoAmp project(path: ":integration-tests", configuration: "amp")
        alfrescoAmp project(path: ":alfresco-health-processor-platform", configuration: "amp")
        alfrescoAmp "eu.xenit.alfred.telemetry:alfred-telemetry-platform:${alfredTelemetryVersion}@amp"
        //Alfresco-share-services amp is only used to populate the test-site that is used in the integration tests.
        alfrescoAmp "org.alfresco:alfresco-share-services:7.0.1.3@amp"
        //OOTB tools is used for troubleshooting/loggers
        alfrescoAmp 'org.orderofthebee.support-tools:support-tools-repo:1.2.2.0:amp@amp'
        alfrescoSM(group: 'io.micrometer', name: "micrometer-core", version: "${micrometerVersion}")
        alfrescoSM(group: 'io.github.mweirauch', name: 'micrometer-jvm-extras', version: "${jvmExtrasVersion}")
                {
                    exclude group: "org.slf4j", module: "*"
                }
    }

    dockerBuild {
        alfresco {
            baseImage = "${alfrescoBaseImage}"
            leanImage = false
        }
    }

    dockerCompose {
        ignorePullFailure = true
        useComposeFiles = [
                "${project.parent.projectDir}/src/test/resources/compose/${integrationTestComposeFile}",
        ]

//        scale = [alfresco: 2]
        ignorePullFailure = false

        removeVolumes = false
        stopContainers = true
        retainContainersOnStartupFailure = true
        captureContainersOutput = false

        // Uncomment for quick iterations when developing integration tests
        // stopContainers = false

        environment.put 'COMPOSE_INFLOW_TCP_8080', project.findProperty('random_ports') ? '8080' : '8080:8080'

        assert project.hasProperty("shareVersion")
        environment.put 'SHARE_VERSION', project.findProperty('shareVersion')
        isRequiredBy(project.tasks.integrationTest)
    }
}
